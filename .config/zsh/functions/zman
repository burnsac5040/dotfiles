# desc: Searches zshall with special keyword ($1) matching

zmp() { man -P "less -p'^${@}'" zshall; }
zmo() { man -P "less -p'^${@}'" zshmodules; }
zb() { man -P "less -p'^       "$1" '" zshbuiltins; }

# zp() { man -P "less -p'^       "$1" '" zshexpn; }
# zs() { man -P "less -p'^       "$1" '" zshparam; }

zp() { man -P "less -p'^       ${1/#[ewspfrRiInbkK]/"${1}"} '" zshexpn; }
zs() { man -P "less -p'^       ${1/#[AacCefFikLnoOPqQtuUvVwWXzpjslrSIBEMNR]/"${1}"} '" zshparam; }

case "${1}" in
  test|cond) zmp "CONDITIONAL EXPRESSIONS$" ;;
  eg|exmple|examples) zmp "   Examples$" ;;
  pexpf|"param exp flags"|"pexp flags") zmp "   Parameter Expansion Flags$" ;;
  pexp|"param exp") zmp "PARAMETER EXPANSION$" ;;
  hexp|"hist exp") zmp "HISTORY EXPANSION$" ;;
  hflag|"hist flags") zmp "   Modifiers$" ;;
  array|"array param") zmp "ARRAY PARAMETERS$" ;;
  arsub|"arr sub"|"array subscripts") zmp "   Array Subscripts$" ;;
  arsubf|"arr sub flags"|"arr flags"|"array flags") zmp "   Subscript Flags$" ;;
  complex|compcmds) zmp "COMPLEX COMMANDS$" ;;
  glob|"glob flags") zmp "   Globbing Flags$" ;;
  opts|options) zmp "DESCRIPTION OF OPTIONS$" ;;
  sopts|1opts) zmp "SINGLE LETTER OPTIONS$" ;;
  prompt|"prompt chars") zmp "SIMPLE PROMPT ESCAPES$" ;;
  redir|red|redirection) zmp "REDIRECTION$" ;;
  bind|bindkey) zmp "STANDARD WIDGETS$" ;;
  zparse|parse) zmm "       zparseopts$" ;;
  b|built) zb ${2} ;;
  p|param) zp ${2} ;;
  s|subscript|sub) zs ${2} ;;
  h|help)
    print -P "%F{11}test, cond -- [[ -e ]]%f"
    print -P "%F{12}eg, examples%f"
    print -P "%F{11}pexpf, param exp -- \"\$ {(f)foo}\"%f"
    print -P "%F{4}pexp, paramexp -- \"\$ {~foo}\"%f"
    print -P "%F{12}hexp, hist exp -- $ {var:h3}%f"
    print -P "%F{11}hflag, hist flags%f"
    print -P "%F{12}array, array param%f"
    print -P "%F{11}arsub, array subscripts%f"
    print -P "%F{12}arsubf, array flags -- $ {array[(i)pattern]}%f"
    print -P "%F{11}complex, compcmds%f"
    print -P "%F{12}glob, glob flags -- (#X)%f"
    print -P "%F{11}opts, options -- AUTO_CD%f"
    print -P "%F{12}sopts, 1opts%f"
    print -P "%F{11}prompt, prompt chars -- %%B%f"
    print -P "%F{12}redir, redirection%f"
    print -P "%F{11}bind bindkey -- vi-backward-kill%f"
    print -P "%F{12}zparse, parse%f"
    print -P "%F{11}b, built -- echoti%f"
    print -P "%F{12}p, param -- $ {var:h}, $ {(f)foo}%f"
    print -P "%F{11}s, subscript -- $ {foo[(re)bar]}%f" ;;
  *) man -P "less -p'^ *$@ '" zshall ;;
esac

unset -f zmp zmo zb zp zs

# vim:ft=zsh:et
